PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:  <http://www.w3.org/2001/XMLSchema#>
PREFIX fx:   <http://sparql.xyz/facade-x/ns/>
PREFIX xyz:  <http://sparql.xyz/facade-x/data/>
PREFIX mtp:  <http://w3id.org/polifonia/ontology/meetups-ontology#>
PREFIX meetup: <http://w3id.org/polifonia/pilot/meetups/>
PREFIX core: <https://w3id.org/polifonia/ontology/core/> 
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX geo: <https://www.w3.org/2003/01/geo/wgs84_pos>

CONSTRUCT {
	?personIRI a mtp:Participant ;
		rdfs:seeAlso core:Person ;
		mtp:hasDBPediaEntity ?personIRI .
	?subjectIRI a mtp:Participant ;
		rdfs:seeAlso core:Person ;
		mtp:hasDBPediaEntity ?subjectIRI ;
		mtp:hasRole ?participantRole .
	?purposeIRI a mtp:Purpose ;
		rdfs:label ?themeType .
	?placeIRI a mtp:Location ;
		rdfs:seeAlso core:Place ;
		geo:lat ?latitude ;
		geo:long ?longitude ;
		mtp:hasDBPediaEntity ?placeIRI ;
		rdfs:label ?label ;
		mtp:thumbnail ?placeImage .
	?beginning_IRI a time:GeneralDateTimeDescription ;
		time:numericDuration ?fValue ;
		time:unitType ?fUnit .
	?end_IRI a time:GeneralDateTimeDescription ;
		time:numericDuration ?tValue ;
		time:unitType ?tUnit .
	?timeExpression_IRI a mtp:TimeExpression ;
		#time:hasBeginning ?beginning_IRI ;
		#time:hasEnd ?end_IRI ;
		mtp:hasEvidenceText ?timeEvidenceText .
	?meetupIRI a mtp:Meetup ;
		mtp:hasParticipant ?personIRI ;
		mtp:hasEvidenceText ?sentenceText ;
		mtp:hasAPurpose ?purposeIRI ;
		mtp:hasConfidence ?valueTheme ;
		mtp:hasPlace ?placeIRI ;
		mtp:happensAt ?timeExpression_IRI ;
		mtp:hasSubject ?subjectIRI .
} WHERE {
	# 
    SERVICE <x-sparql-anything:> {
 	 BIND ( CONCAT ( "../meetupsAnnotations/", ?_fileId , ".csv" ) AS ?location ) .
    	 fx:properties fx:location ?location ; fx:csv.headers "true" .
 	 [] xyz:paragraphIndex ?paragraph ; 
 	    xyz:sentenceIndex ?sentence ;
 		xyz:sentences ?sentenceText .
		# 
	 BIND ( IRI(?_subject) as ?subjectIRI ) .
     BIND ( fx:entity (meetup:, fx:String.toLowerCase(?_fileId), "/", ?paragraph, "/", ?sentence) AS ?meetupIRI ) .
	}
   .
   SERVICE <x-sparql-anything:> {
	 BIND ( CONCAT ( "../extractedEntitiesPersonPlaceOnly/", ?_fileId , ".csv" ) AS ?location1 ) .
   	 fx:properties fx:location ?location1 ; fx:csv.headers "true" .
	 {
	 ?s1 xyz:paragraphIndex ?paragraph ; 
	    xyz:sentenceIndex ?sentence .
	# 
	OPTIONAL { ?s1 xyz:URI ?person ; xyz:entType "person" } .
	OPTIONAL { ?s1 xyz:URI ?place ; xyz:entType "place" } .
   }
   UNION {
       SERVICE <x-sparql-anything:> {
    	 BIND ( CONCAT ( "../meetupsCorefOutputPP/", ?_fileId , ".csv" ) AS ?location2 ) .
       	 fx:properties fx:location ?location2 ; fx:csv.headers "true" .
    	 ?s1 xyz:paragraphIndex ?paragraph ;
    	  	 xyz:sentenceIndex ?sentence .
    	# 
    	OPTIONAL { ?s1 xyz:URI ?person ; xyz:entType "person" } .
    	OPTIONAL { ?s1 xyz:URI ?place ; xyz:entType "place" } .
    	# 
       }
   }
   
 # 
 	BIND ( IRI(?person) as ?personIRI ) .
	BIND ( IRI(?place) as ?placeIRI ) .
    BIND ( fx:entity (meetup:, fx:String.toLowerCase(?_fileId), "/", ?paragraph, "/", ?sentence) AS ?meetupIRI ) .
   }
   .
   SERVICE <x-sparql-anything:> {
	 BIND ( CONCAT ( "../extractedTimeExpressions/", ?_fileId , ".csv" ) AS ?location3 ) .
   	 fx:properties fx:location ?location3 ; fx:csv.headers "true" .
	 ?s3 xyz:paragraphIndex ?paragraph ; 
	    xyz:sentenceIndex ?sentence ;
		xyz:time_expression_type ?entType ;
		xyz:time_expression ?timeEvidenceText .
	# 
	 BIND ( IRI ( CONCAT (STR(meetup:), STR( fx:DigestUtils.md5Hex(?timeEvidenceText)))) AS ?timeExpression_IRI ) .
     BIND ( fx:entity (meetup:, fx:String.toLowerCase(?_fileId), "/", ?paragraph, "/", ?sentence) AS ?meetupIRI ) .
   }
   .
   SERVICE <x-sparql-anything:> {
	 BIND ( CONCAT ( "../extractedMeetupTypes/", ?_fileId , ".csv" ) AS ?location4 ) .
   	 fx:properties fx:location ?location4 ; fx:csv.headers "true" .
	 ?s4 xyz:paragraphIndex ?paragraph ; 
	    xyz:sentenceIndex ?sentence ;
		xyz:themeType ?themeType ;
		xyz:value ?valueTheme  .
	# 
	BIND ( IF (?themeType = "Business meeting", "businessMeeting" , IF (?themeType = "Coincidence","coincidence", IF (?themeType = "Education", "education", IF (?themeType = "Music making", "musicMaking", IF (?themeType = "Personal life", "personalLife", IF (?themeType = "Public celebration", "publicCelebration", "")))))) AS ?purposeTypeName ) .
	BIND ( fx:entity (meetup: , fx:String.toLowerCase(?purposeTypeName) ) AS ?purposeIRI ) .
	 # 
     BIND ( fx:entity (meetup:, fx:String.toLowerCase(?_fileId), "/", ?paragraph, "/", ?sentence) AS ?meetupIRI ) .
   }
}